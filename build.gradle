
apply plugin: 'application'

mainClassName = 'com.koval.jresolver.Launcher'

def unixClassPath = 'CLASSPATH=\\$APP_HOME/lib/*:\\$APP_HOME/config:\\$APP_HOME/rules:\\$APP_HOME/data'
def windowsClassPath = 'CLASSPATH=%APP_HOME%/lib/;%APP_HOME%/lib/*;%APP_HOME%/config;%APP_HOME%/rules;%APP_HOME%/data'

run {
  workingDir = file('src')
}

task createDataSet(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['create-data-set']
}

task createVectorModel(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['create-vector-model']
}

task createDocumentationDataSet(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['create-documentation-data-set']
}

task createDocumentationVectorModel(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['create-documentation-vector-model']
}

task createConfluenceDataSet(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['create-confluence-data-set']
}

task createConfluenceVectorModel(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['create-confluence-vector-model']
}

task cleanWorkFolders(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['clean']
}

task printFields(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['print-fields']
}

task testSimilarityProcessor(type: JavaExec) {
  group 'application'
  main = "$mainClassName"
  classpath = sourceSets.main.runtimeClasspath
  workingDir = file('src')
  args = ['test-similarity-processor']
}

sourceSets {
  main {
    resources {
      srcDir 'data'
      srcDir 'docs'
      srcDir 'src/main/resources'
      srcDir 'jira-connector/src/main/resources'
      srcDir 'bugzilla-connector/src/main/resources'
      srcDir 'confluence-connector/src/main/resources'
      srcDir 'similarity-processor/src/main/resources'
      srcDir 'rule-engine-processor/src/main/resources'
      srcDir 'documentation-processor/src/main/resources'
      srcDir 'confluence-processor/src/main/resources'
      srcDir 'html-reporter/src/main/resources'
    }
  }
}

dependencies {
  compile project(':jira-connector'),
      project(':bugzilla-connector'),
      project(':confluence-connector'),
      project(':similarity-processor'),
      project(':rule-engine-processor'),
      project(':documentation-processor'),
      project(':confluence-processor'),
      project(':html-reporter'),
      project(':text-reporter'),
      project(':test-similarity-processor')
}

wrapper {
  gradleVersion = '5.6.4'
}

distributions {
  main {
    baseName = 'jresolver'
    contents {
      from('jira-connector/src/main/resources/jira-connector.properties') { into 'config' }
      from('bugzilla-connector/src/main/resources/bugzilla-connector.properties') { into 'config' }
      from('confluence-connector/src/main/resources/confluence-connector.properties') { into 'config' }
      from('similarity-processor/src/main/resources/similarity-processor.properties') { into 'config' }
      from('documentation-processor/src/main/resources/documentation-processor.properties') { into 'config' }
      from('confluence-processor/src/main/resources/confluence-processor.properties') { into 'config' }
      from('html-reporter/src/main/resources') { into 'config' }
      from('src/main/resources/logback.xml') { into 'config' }
      from('src/main/resources/control.properties') { into 'config' }
      from('rule-engine-processor/src/main/resources') { into 'rules' }
      exclude('**/*-android-*.jar')
      exclude('**/*-macosx-*.jar')
      exclude('**/*-linux-armhf.jar')
      exclude('**/*-linux-ppc64le.jar')
      exclude('**/snakeyaml*.jar')
      exclude('**/sslext*.jar')
      exclude('**/oro*.jar')
      exclude('**/stax2*.jar')
      exclude('**/xpp3*.jar')
      exclude('**/xstream*.jar')
      exclude('**/xmlpull*.jar')
      exclude('**/fastutil*.jar')
      exclude('**/findbugs*.jar')
      exclude('**/freemarker*.jar')
      exclude('**/lombok*.jar')
      exclude('**/neoitertools*.jar')
      exclude('**/protobuf*.jar')
    }
  }
}

startScripts {
  group 'distribution'
  classpath += files('config')
  applicationName = 'run'
  doLast {
    def windowsScriptFile = file getWindowsScript()
    def unixScriptFile = file getUnixScript()
    windowsScriptFile.text = windowsScriptFile.text
        .replaceFirst(/CLASSPATH=\S+/, windowsClassPath)
    unixScriptFile.text = unixScriptFile.text
        .replaceFirst(/CLASSPATH=\S+/, unixClassPath)
  }
}

def actions = [
  [taskName: 'createDataSetScript', scriptName: 'create-data-set'],
  [taskName: 'createVectorModelScript', scriptName: 'create-vector-model'],
  [taskName: 'createDocumentationDataSetScript', scriptName: 'create-documentation-data-set'],
  [taskName: 'createDocumentationVectorModelScript', scriptName: 'create-documentation-vector-model'],
  [taskName: 'createConfluenceDataSetScript', scriptName: 'create-confluence-data-set'],
  [taskName: 'createConfluenceVectorModelScript', scriptName: 'create-confluence-vector-model'],
  [taskName: 'cleanScript', scriptName: 'clean'],
  [taskName: 'printFieldsScript', scriptName: 'print-fields']
  // [taskName: 'testSimilarityProcessorScript', scriptName: 'test-similarity-processor']
]

actions.each { action ->
  task "${action.taskName}"(type: CreateStartScripts) {
    group 'distribution'
    mainClassName = startScripts.mainClassName
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = "${action.scriptName}"

    "${action.taskName}" {
      doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile.text
            .replaceFirst(/CLASSPATH=\S+/, windowsClassPath)
        unixScriptFile.text = unixScriptFile.text
            .replaceFirst(/CLASSPATH=\S+/, unixClassPath)
        windowsScriptFile.text = windowsScriptFile.text
            .replaceFirst('CMD_LINE_ARGS=', "CMD_LINE_ARGS=${action.scriptName}")
        unixScriptFile.text = unixScriptFile.text
            .replaceFirst('APP_ARGS=', "APP_ARGS=${action.scriptName}")
      }
    }
  }
}

applicationDistribution.into('bin') {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  actions.each { action ->
    from(tasks.getByName(action.taskName))
  }
  fileMode = 0755
}

jar {
  exclude('*.xml', '*.zip', '*.txt', '*.properties', '*.vm', '*.drl')
}

defaultTasks 'build'

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'eclipse'

  group = 'com.koval.jresolver'
  sourceCompatibility = 1.8

  repositories {
    mavenCentral()
    maven {
      url 'https://m2proxy.atlassian.com/repository/public'
    }
    maven {
      url 'http://repository.jboss.org/nexus/content/groups/public-jboss'
    }
  }

  compileJava {
    options.compilerArgs << '-Xlint:deprecation'
  }

  apply plugin: 'checkstyle'
  checkstyle {
    configFile = rootProject.file('config/checkstyle.xml')
    toolVersion = '8.1'
    ignoreFailures = false
  }

  tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
      reports.all { report ->
        def outputFile = report.destination
        if (outputFile.exists() && outputFile.text.contains("<error ")) {
          throw new GradleException("Found checkstyle issues in $outputFile")
        }
      }
    }
  }

  apply plugin: 'pmd'
  pmd {
    ruleSetFiles = rootProject.files('config/pmd.xml')
    toolVersion = '5.4.1'
    ignoreFailures = false
  }

  /*
  apply plugin: 'findbugs'
  findbugs {
    toolVersion = '3.0.1'
    excludeFilter = rootProject.file('config/findbugs_filter.xml')
    ignoreFailures = false
  }

  tasks.withType(FindBugs) {
    reports {
      xml.enabled = false
      html.enabled = true
    }
  }
  */

  apply from: rootProject.file('jdepend.gradle')
  apply from: rootProject.file('coverage.gradle')

  task cleanOut(type: Delete) {
    group 'build'
    delete "${projectDir}/out"
  }
  tasks.clean.dependsOn(cleanOut)
}
